import os
import sys

Import('szgEnv','externalFlags')

extPath = os.environ['SZGEXTERNAL'].replace('\\','/')+'/'+sys.platform+'/'


# parameters specific to this library
name = 'bird'
includePath = extPath+'ascension-mingw/include'
libPath = extPath+'ascension-mingw/lib'
ccFlag = '-D EnableBirdWinDriver'
libName = 'Bird'
headerName = 'Bird.h'
checkFailMessage = "Didn't find optional Flock of Birds library or header; BirdWinDriver disabled."

# boileplate code that should be pretty much common to all libraries
# (may factor it out later).
flags = {
    'CPPPATH':[includePath], 'LIBPATH':[libPath], 'LIBS':[libName]
    }

if ccFlag:
  flags['CCFLAGS'] = [ccFlag]

testEnv = szgEnv.Clone()
testEnv.MergeFlags( flags )
conf = Configure( testEnv )
if not conf.CheckLibWithHeader( libName, headerName, 'c' ):
  print checkFailMessage
  externalFlags[name] = {}
else:
  externalFlags[name] = flags
conf.Finish()

